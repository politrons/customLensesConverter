

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

allprojects {
    group = 'com.datamountaineer'
    version = project.version
    description = "stream-reactor"
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        scala = '2.12.11'
        kcqlVersion = "2.8.7"
        kafkaVersion = '2.5.0'
        gitCommitHash = ("git rev-parse HEAD").execute().text.trim()
        gitTag = ("git describe --abbrev=0 --tags").execute().text.trim()
        gitRepo = ("git remote get-url origin").execute().text.trim()
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://packages.confluent.io/maven/" }
        maven { url "https://repo.typesafe.com/typesafe/releases/" }
        maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
        maven { url "https://conjars.org/repo" }
        maven { url "https://jitpack.io"}
    }

    shadowJar {

        manifest {
            attributes( "StreamReactor-Version": project.version,
                    "KCQL-Version" : "${kcqlVersion}",
                    "Kafka-Version": "${kafkaVersion}",
                    "Created-By" : "Lenses",
                    "Created-At" : new Date().format("YYYYMMDDHHmm"),
                    "Git-Repo": "${gitRepo}",
                    "Git-Commit-Hash": "${gitCommitHash}",
                    "Git-Tag": "${gitTag}",
                    "StreamReactor-Docs" : "https://docs.lenses.io/connectors/"
            )
        }
    }

    dependencies {
        implementation 'io.confluent:kafka-connect-avro-converter:5.0.0'
        implementation 'com.datamountaineer:kafka-connect-ftp:1.2.0'
        implementation "com.jcraft:jsch:0.1.55"
    }

}


project.tasks.compileScala.scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]
project.tasks.compileTestScala.scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]
